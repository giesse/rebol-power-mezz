HTML Filter

    Type: Module
    Purpose: {
        Filters HTML text removing any potential security treat;
        allows embedding some HTML coming from an untrusted source
        in a web page without creating security holes for the web site.
    }
    Author: "Gabriele Santilli"
    File: %filter-html.r
    Version: 3.0.0
    Imports: [
        %dialects/fsm.r         "Used for the state machine"
        %parsers/common-rules.r "We need the NAME rule"
        %parsers/rule-arguments.r "We need POP-RESULT"
        %parsers/ml-parser.r    "Used to parse the HTML text"
        %mezz/normalize-html.r  "Used to normalize the HTML before regenerating it"
        %parsers/uri-parser.r   "Used to check and normalize the URLs"
        %dialects/emit.r        "Used to generate output"
        %dialects/ml-emitter.r  "Used to generate HTML"
        %mezz/trees.r           "Used to represent the HTML document and for tree rewriting"
        %parsers/css-parser.r   "Used to parse the style attribute"
    ]
    Globals: [
        filter-html             "Made global so that it's easy to use in non-modules"
    ]

===Introduction

It is often desirable, for certain kinds of web sites (wikis, forums, blogs, and so on),
to allow advanced users to customize any text input using HTML tags; in some cases,
where the site allows users to publish new pages or edit existing content, allowing
users to edit the HTML directly is even more desirable. However, if you can't trust the
users, allowing HTML means opening up to a number of vulnerabilities (especially cross-site
scripting, phishing, etc.); the solution to the problem is to "sanitize" any HTML text
coming from an untrusted source before embedding it in the final web page. If all potentially
harmful content is removed, it is possible to safely embed the HTML into any page.

===Overview

This program defines the |filter-html| function, that can be used to sanitize any HTML
text. The string passed as argument is fully parsed and completely regenerated as
XHTML 1.0 Strict, which is returned as a string.

If the |/all| refinement is used, a complete XHTML 1.0 document is returned; otherwise,
the text returned contains only the "body" part of the document and can be embedded
directly inside another XHTML document.

More options can be specified with the |/with| refinement, followed by a block
containing |set-word!|'s and values. See below for the list of available options
you can set.

\note Note:

The source |html| string is assumed to use the UTF-8 encoding. If your text uses any
other encoding, you **must** convert it to UTF-8 before calling this function.

The output string will always be ASCII (all non-ASCII characters will be converted
to a character entity), unless the |emit-utf8| option is set to |true|, in which case the output
string will be UTF-8 encoded.

/note

    -main-:
    -stage3-

    filter-html: func [
        "Sanitize HTML text"
        html [string!]
        /all "Return a complete HTML document (including <html> tag, <head> etc.)"
        /with options [block!] "Specify options for the filter"
    ] [
        -filter-html-
    ]

---Filtering options

The |/with| refinement of |filter-html| allows passing a block of |set-word!|'s to set
specific filtering options. The available options are:

:|all| - equivalent to the |/all| refinement (|logic!|).

:|id-prefix| - Allows specifying a prefix to be prepended to all tag IDs (or anchor names,
and so on) that are specified in the source document (thus preventing clashes with IDs
in the host document). If you are embedding the filtered HTML in a document that makes use
of IDs, anchors etc., then you **should** specify an |id-prefix| that
ensures no clashes. (Note: currently the CSS is not being parsed, so references to IDs in
the CSS will break when using this is used.) If specified, it must be a |string!| value,
and be valid as an HTML tag ID encoded in UTF-8.

:|emit-utf8| - can be used to disable the conversion of all non-ASCII characters
to HTML entities (e.g. if your page is in Japanese then you don't want every character
to be encoded as an HTML entity, as that would take much more space than UTF-8). Should
be a |logic!| value.

:|filter-uris| - allows specifying a handler function for all URIs that appear in the document.
See |-uri-filtering-| for more details.

:|target| - can be used to tweak the filtering for a specific target. The default is |xhtml|
which produces generic, cross-browser XHTML 1.0 code. Currently, the only other supported
target is |editor| which makes the output suitable for usage with Steve Ewing's Free Rich Text
Editor.

    -filter-options-:
    default-options: context [
        all: no
        id-prefix: none
        emit-utf8: no
        filter-uris: none
        target: 'xhtml
    ]

===URI filtering

You can provide a filter function for all URIs in the HTML document via the |filter-uris| option.
The option must be set to a function taking one object as argument and returning |none|, a string, or
an object. The object passed as argument is what is returned by |parse-uri|, with the addition of two
fields: |tag-name| and |attribute-name|, set to the name of the tag (a string, that may end in "/"
for empty tags) and the name of the attribute (as a |word!|) the URI is in.

If your function returns |none|, the attribute is discarded. If a |string!| value is returned, that
is used as the value for the attribute (should be a correctly formed URI); otherwise, you can return
an object that is passed to |form-uri| (for eg. you can just modify the object passed as argument and
return it).

    -uri-filtering-:
    uri-filter: func [uri] [
        case [
            all [uri/tag-name = "img" uri/attribute-name = 'src] [
                ; disable images
                none
            ]
            uri/host = "www2.site.com" [
                ; replace domain
                uri/host: "www.site.com"
                uri
            ]
            uri/scheme = "javascript" [
                ; disable javascript: urls
                none
            ]
            ; and so on...
        ]
    ]
    filter-html/with {...} [
        filter-uris: :uri-filter
    ]

===Parse and regenerate the |html| text

Parsing and regeneration is done using a multi-stage pipeline of
finite state machines (FSMs). The first stage uses |parse-ml| on
the source text, parsing tags, text and character entities,
and sends the parsed elements to the second stage, the |html-normalizer| module,
via the |process-tag| function; that in turn sends the normalized elements
to the |stage3| function, which builds a tree to represent the whole document.
The tree is then processed using the |rewrite-tree| function from the |trees| module,
and then converted back to XHTML.

    -filter-html-:
    options: make default-options any [options [ ]]
    if all [options/all: yes]
    init-stage3 options
    init-normalizer :stage3
    parse-ml html :process-tag
    reset-normalizer
    end-stage3

===Tree generation

In this stage, we use a FSM to generate a tree from the output of the HTML Normalizer.
The |stage3| function just calls |process-event|; |init-stage3| takes care of the initialization
of this stage, while |end-stage3| resets the state machine, applies the tree rewriting rules
to the generated tree, and finally converts it back to XHTML text.

    -stage3-:
    -filter-options-
    stage3: func [cmd data] [
        process-event stage3-fsm cmd data
    ]
    init-stage3: func [options] [
        root: current-node: make-node 'root
        current-options: options
        filter-uris?: any-function? get in current-options 'filter-uris
        reset-fsm/only stage3-fsm
    ]
    end-stage3: does [
        reset-fsm stage3-fsm
        -tree-rewriting-
        -emitter-
    ]
    -state-machine-
    -emitter-functions-

---Finite State Machine to generate a tree from the parsed and normalized HTML

    -state-machine-:
    -fsm-support-functions-
    stage3-fsm: make-fsm [
        initial-state: [
            ; whitespace before <html> removed
            comment: (make-comment-node data)
            <html> (
                enter-tag 'html data [xmlns force http://www.w3.org/1999/xhtml]
            ) in-html (
                leave-tag
            )
        ]
        common: [
            comment: (make-comment-node data)
            whitespace: (make-text-node data)
        ]
        in-html: inherit common [
            <head> (enter-tag 'head data [ ]) in-head (leave-tag)
            <body> (
                enter-tag 'body data [
                    background [style* uri   "background-image"] none
                    text       [style* color "color"]            none
                    link       color                             none
                    vlink      color                             none
                    alink      color                             none
                ]
            ) in-body (leave-tag)
            </html> return
        ]
        in-head: inherit common [
            </head> return
            <title> (enter-tag 'title data [ ]) in-title (leave-tag)
            <base/> (
                make-tag-node 'base data [
                    href   uri             none
                    target [enum "_blank"] none
                ]
            )
            <style> (
                enter-tag 'style data [
                    type  force      "text/css"
                    media media-desc none
                ]
            ) in-style (leave-tag)
            ;<meta/>
            <link/> (
                make-tag-node 'link data [
                    href     uri             none
                    hreflang name*           none
                    type     cdata           none
                    rel      name-list       none
                    rev      name-list       none
                    charset  cdata           none
                    target   [enum "_blank"] none
                    media    media-desc      none
                ]
            )
        ]
        in-style: [
            text: whitespace: (make-text-node data)
            </style> return
        ]
        in-title: [
            text: whitespace: (make-text-node data)
            </title> return
        ]
        in-body: inherit common [
            </body> return

            <style> (
                ; move to HEAD
                save-node: current-node
                current-node: select-child root 'html/head
                enter-tag 'style data [
                    type  force      "text/css"
                    media media-desc none
                ]
            ) in-style (leave-tag current-node: save-node)

            <h1> <h2> <h3> <h4> <h5> <h6> <address> <p>
            <pre> <legend>
                (enter-tag tag-name? event data [ ]) in-para (leave-tag)

            <ul> (
                if current-options/target = 'editor [
                    data: reduce [
                        'class select data 'class
                    ]
                ]
                enter-tag 'ul data [
                    type  [style* [enum "disc" "circle" "square"] "list-style-type"] none
                ]
            ) in-list (leave-tag)
            <ol> (
                if current-options/target = 'editor [
                    data: reduce [
                        'class select data 'class
                    ]
                ]
                enter-tag 'ol data [
                    type  [style* list-style "list-style-type"] none
                    start number                                none
                ]
            ) in-list (leave-tag)
            <dl> (enter-tag 'dl data [ ]) in-dlist (leave-tag)

            <div> (
                created: 1
                valid-attrs: [ ]
                if current-options/target = 'editor [
                    either div-style: select data 'style [
                        div-style: parse-css div-style
                        switch select div-style 'margin-left [
                            "40px" [
                                enter-tag 'blockquote [ ] [ ]
                                created: 2
                            ]
                            "80px" [
                                enter-tag 'blockquote [ ] [ ]
                                enter-tag 'blockquote [ ] [ ]
                                created: 3
                            ]
                        ]
                        data: reduce [
                            'align any [select div-style 'text-align select data 'align]
                        ]
                    ] [
                        data: reduce ['align select data 'align]
                    ]
                    valid-attrs: [
                        align [enum "left" "center" "right" "justify"] none
                    ]
                ]
                enter-tag 'div data valid-attrs
                set-property current-node 'created created
            ) in-body (
                created: get-property current-node 'created
                set-property current-node 'created none
                loop created [leave-tag]
            )
            <fieldset> (enter-tag 'fieldset data [ ]) in-body (leave-tag)
            <center> (enter-tag 'div append copy [align "center"] data [ ]) in-body (leave-tag)

            <blockquote> (enter-tag 'blockquote data [cite uri none]) in-body (leave-tag)
            <form>  (enter-tag 'form data [
                action  force "#" ; disable forms!
                method  force "GET"
                name    name* none
            ]) in-body (leave-tag)
            <table> (enter-tag 'table data [
                summary     cdata                          none
                align       [enum "left" "center" "right"] none
                width       [style* lengthpx "width"]      none
                frame       [
                    enum "void" "above" "below" "hsides"
                        "lhs" "rhs" "vsides" "box" "border"
                ]                                          none
                rules       [
                    enum "none" "groups" "rows" "cols" "all"
                ]                                          none
                border      [style* pixels "border-width"] none
                cellspacing length                         none
                cellpadding length                         none
            ]) in-table (leave-tag)
            
            <ins> <del> (enter-tag tag-name? event data [
                cite     uri   none
                datetime cdata none
            ]) in-body (leave-tag)

            <hr/> (make-tag-node 'hr data [
                ; this is quite hard to convert to CSS automatically
                ; so will keep the deprecated attributes
                align   [enum "left" "right" "center"] none
                noshade [bool "noshade"]               none
                size    number                         none
                width   length                         none
            ])
            
            </div> </blockquote> </form> </fieldset> </ins> </del> </center> return
        ]
        in-para: inherit common [
            </h1> </h2> </h3> </h4> </h5> </h6> </address> </p>
            </dt> </pre> </caption> </legend> </font>
            </tt> </i> </b> </u> </strike> </s> </big> </small> </sub> </sup>
            </em> </strong> </dfn> </code> </samp> </kbd> </var> </cite>
            </a> </map> </button> </label> </span>
            </abbr> </acronym> </q> ;</object>
            </ins> </del> ; should be treated specially, but noone uses them so...
                return

            <tt> <i> <b> <u> <strike> <s> <big> <small> <sub> <sup>
            <em> <strong> <dfn> <code> <samp> <kbd> <var> <cite>
            <abbr> <acronym>
                (enter-tag tag-name? event data [ ]) in-para (leave-tag)

            <a> (enter-tag 'a data [
                name     name*                                   none
                href     uri                                     none
                hreflang name*                                   none
                type     cdata                                   none
                rel      name-list                               none
                rev      name-list                               none
                charset  cdata                                   none
                target   [enum "_blank"]                         none
                shape    [enum "default" "rect" "circle" "poly"] none
                coords   cdata                                   none
            ]) in-para (leave-tag)

            <font> (enter-tag 'span data [
                size  [style* font-size "font-size"]   none
                color [style* color     "color"]       none
                face  [style* cdata     "font-family"] none
            ]) in-para (leave-tag)

            <span> (
                created: 1
                if current-options/target = 'editor [
                    data: reduce [
                        'style select data 'style
                        'class select data 'class
                    ]
                ]
                enter-tag 'span data [ ]
                span-node: current-node
                if span-style: get-property span-node 'style [
                    span-style: parse-css span-style
                    if "bold" = select span-style 'font-weight [
                        created: 2
                        enter-tag 'b [ ] [ ]
                        span-style/font-weight: none
                    ]
                    if "italic" = select span-style 'font-style [
                        created: created + 1
                        enter-tag 'i [ ] [ ]
                        span-style/font-style: none
                    ]
                ]
                set-property current-node 'created created
                if span-style [
                    if current-options/target = 'editor [
                        span-style: reduce [
                            'text-decoration attr-types/enum select span-style 'text-decoration ["underline" "line-through"]
                        ]
                    ]
                    span-style: form-css span-style
                    set-property span-node 'style either empty? span-style [none] [span-style]
                ]
            ) in-para (
                created: get-property current-node 'created
                set-property current-node 'created none
                loop created [leave-tag]
            )

            <map> (enter-tag 'map data [name name* none]) in-para (leave-tag)
            <area/> (make-tag-node 'area data [
                shape    [enum "default" "rect" "circle" "poly"] none
                coords   cdata                                   none
                nohref   [bool "nohref"]                         none
                alt      cdata                                   none
                href     uri                                     none
                target   [enum "_blank"]                         none
            ])

            <select> (enter-tag 'select data [
                name     name*             none
                size     number            none
                multiple [bool "multiple"] none
                disabled force             "disabled"
            ]) in-select (leave-tag)
            <textarea> (enter-tag 'textarea data [
                name     name*             none
                rows     number            none
                cols     number            none
                readonly [bool "readonly"] none
                disabled force             "disabled"
            ]) in-option (leave-tag)
            <button> (enter-tag 'button data [
                name     name*                            none
                value    cdata                            none
                type     [enum "submit" "button" "reset"] none
                disabled force                            "disabled"
            ]) in-para (leave-tag)
            <label> (enter-tag 'label data [for name* none]) in-para (leave-tag)
            <input/> (make-tag-node 'input data [
                type      [
                    enum "text" "password" "checkbox" "radio"
                        "submit" "reset" "file" "hidden"
                        "image" "button"
                ]                           none
                name      name*             none
                value     cdata             none
                size      number            none
                maxlength number            none
                checked   [bool "checked"]  none
                src       uri               none
                alt       cdata             none
                accept    cdata             none
                readonly  [bool "readonly"] none
                disabled  force             "disabled"
                usemap    uri               none
                ismap     [bool "ismap"]    none
            ])

            <q> (enter-tag 'q data [cite uri none]) in-para (leave-tag)

            ;<object>
            
            <ins> <del> (enter-tag tag-name? event data [
                cite     uri   none
                datetime cdata none
            ]) in-pana (leave-tag)

            <img/> (make-tag-node 'img data [
                src      uri            none
                alt      cdata          none
                longdesc uri            none
                name     name*          none
                usemap   uri            none
                ismap    [bool "ismap"] none
                width    length         none
                height   length         none
                hspace   [imgmargin "left" "right"] none
                vspace   [imgmargin "top" "bottom"] none
                border   [style* pixels "border-width"] none
                align    imgalign       none
            ])

            <br/> (make-tag-node 'br data [clear brclear none])

            ;<basefont/>

            text: (make-text-node data)
        ]
        in-item: inherit in-body inherit in-para [
            </li> </td> </th> </dd> return
        ]
        in-list: inherit common [
            <li> (
                if current-options/target = 'editor [
                    data: reduce [
                        'class select data 'class
                    ]
                ]
                enter-tag 'li data [
                    type  [style* list-style "list-style-type"] none
                    value number                                none
                ]
            ) in-item (leave-tag)

            <ul> (
                ;enter-tag 'li [ ] [ ]
                if current-options/target = 'editor [
                    data: reduce [
                        'class select data 'class
                    ]
                ]
                enter-tag 'ul data [
                    type  [style* [enum "disc" "circle" "square"] "list-style-type"] none
                ]
            ) in-list (leave-tag) ;(leave-tag leave-tag)
            <ol> (
                ;enter-tag 'li [ ] [ ]
                if current-options/target = 'editor [
                    data: reduce [
                        'class select data 'class
                    ]
                ]
                enter-tag 'ol data [
                    type  [style* list-style "list-style-type"] none
                    start number                                none
                ]
            ) in-list (leave-tag) ;(leave-tag leave-tag)

            </ul> </ol> return
        ]
        in-dlist: inherit common [
            <dt> (enter-tag 'dt data [ ]) in-para (leave-tag)
            <dd> (enter-tag 'dd data [ ]) in-item (leave-tag)

            </dl> return
        ]
        in-table: inherit common [
            <caption> (enter-tag 'caption data [ ]) in-para (leave-tag)

            <colgroup> (enter-tag 'colgroup data [
                span   number       none
                width  multi-length none
                valign [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ]) in-colgroup (leave-tag)
            <col/> (make-tag-node 'col data [
                span   number       none
                width  multi-length none
                valign [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ])

            <thead> <tfoot> <tbody> (enter-tag tag-name? event data [
                valign [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ]) in-rows (leave-tag)

            <tr> (enter-tag 'tbody [ ] [ ]) continue in-rows (leave-tag)

            </table> return
        ]
        in-colgroup: inherit common [
            <col/> (make-tag-node 'col data [
                span   number       none
                width  multi-length none
                valign [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ])
            
            </colgroup> return
        ]
        in-rows: inherit common [
            <tr> (enter-tag 'tr data [
                valign [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ]) in-cells (leave-tag)
            
            </thead> </tfoot> </tbody> return

            </table> 2 return ; no <tbody>
        ]
        in-cells: inherit common [
            <td> <th> (enter-tag tag-name? event data [
                headers name-list                                none
                scope   [enum "row" "col" "rowgroup" "colgroup"] none
                abbr    cdata                                    none
                axis    cdata                                    none
                rowspan number                                   none
                colspan number                                   none
                nowrap  [style* [bool "nowrap"] "white-space"]   none
                width   [style* lengthpx "width"]                  none
                height  [style* lengthpx "height"]                 none
                ; char alignment not supported (browsers do not support it anyway)
                valign  [style* [enum "baseline" "top" "bottom" "middle"] "vertical-align"] none
            ]) in-item (leave-tag)

            </tr> return
        ]
        in-select: inherit common [
            <option> (enter-tag 'option data [
                selected [bool "selected"] none
                value    cdata             none
                label    cdata             none
                disabled force             "disabled"
            ]) in-option (leave-tag)
            <optgroup> (enter-tag 'optgroup data [
                label    cdata none
                disabled force "disabled"
            ]) in-optgroup (leave-tag)

            </select> return
        ]
        in-option: inherit common [
            text: (make-text-node data)
            
            </option> </textarea> return
        ]
        in-optgroup: inherit common [
            <option> (enter-tag 'option data [
                selected [bool "selected"] none
                value    cdata             none
                label    cdata             none
                disabled force             "disabled"
            ]) in-option (leave-tag)

            </optgroup> return
        ]
    ]

---Support functions used by the FSM

    -fsm-support-functions-:
    tag-name?: func [tag] [
        to word! lowercase as-string tag
    ]
    make-tag-node: func [name attributes valid-attributes /local node value uris] [
        append-node current-node node: make-node name
        if not block? attributes [attributes: [ ]]
        set-property node 'lang any [
            process-attr select attributes 'xml/lang 'name* none
            process-attr select attributes 'lang     'name* none
        ]
        style: make string! 256
        uris: clear [ ]
        foreach [attr-name type defvalue] union/skip valid-attributes global-attrs 3 [
            set-property node attr-name value: process-attr select attributes attr-name type defvalue
            if all [type = 'uri value] [
                append uris attr-name
            ]
        ]
        foreach uri uris [
            either filter-uris? [
                value: current-options/filter-uris make get-property node uri [
                    tag-name: name
                    attribute-name: uri
                    target: get-property node 'target
                ]
                either object? value [
                    set-property node uri form-uri value
                    set-property node 'target value/target
                ] [
                    set-property node uri value
                ]
            ] [
                value: get-property node uri
                set-property node uri either find [#[none] "http" "https" "ftp" "telnet" "news" "mailto" "nntp" "gopher"] value/scheme [
                    form-uri value
                ] [
                    none
                ]
            ]
        ]
        if value: select attributes 'style [
            append style value
        ]
        if not empty? style [
            set-property node 'style style
        ]
        node
    ]
    enter-tag: func [name attributes valid-attributes] [
        current-node: make-tag-node name attributes valid-attributes
    ]
    leave-tag: does [
        current-node: parent-node? current-node
    ]
    make-text-node: func [text /local node out] [
        either all [node: last-child? current-node 'text = node-type? node] [
            out: get-property node 'value
            unless get-property node 'copied? [
                set-property node 'copied? true
                set-property node 'value out: copy out
            ]
            append out text
            node
        ] [
            append-node current-node node: make-node 'text
            set-property node 'value text
            node
        ]
    ]
    make-comment-node: func [comment /local node] [
        append-node current-node node: make-node 'comment
        set-property node 'value comment
        node
    ]
    global-attrs: [
        dir     [enum "LTR" "RTL"]                none
        id      name*                             none
        class   name-list                         none
        title   cdata                             none
        bgcolor [style* color "background-color"] none
        align   [style* [enum "left" "center" "right" "justify"] "text-align"] none
    ]
    attr-types: context [
        enum: func [value opts] [
            if value: find opts value [value/1]
        ]
        name*: func [value opts] [
            if parse/all trim/lines value [name] [value]
        ]
        name-list: func [value opts] [
            if parse/all trim/lines value [name any [" " name]] [
                value
            ]
        ]
        force: func [value opts] [none] ; force default
        style*: func [value opts] [
            if value: process-attr value opts/1 none [
                repend style either opts/1 = 'uri [
                    [opts/2 ": url('" form-uri value "');"]
                ] [
                    [opts/2 ": " value ";"]
                ]
            ]
            none
        ]
        color: func [value opts] [
            if parse/all trim/lines value [
                "#" 3 6 hexdigit
                |
                "aliceblue" | "antiquewhite" | "aqua" | "aquamarine" | "azure" |
                "beige" | "bisque" | "black" | "blanchedalmond" | "blue" |
                "blueviolet" | "brown" | "burlywood" | "cadetblue" | "chartreuse" |
                "chocolate" | "coral" | "cornflowerblue" | "cornsilk" | "crimson" |
                "cyan" | "darkblue" | "darkcyan" | "darkgoldenrod" | "darkgray" |
                "darkgreen" | "darkkhaki" | "darkmagenta" | "darkolivegreen" |
                "darkorange" | "darkorchid" | "darkred" | "darksalmon" |
                "darkseagreen" | "darkslateblue" | "darkslategray" | "darkturquoise" |
                "darkviolet" | "deeppink" | "deepskyblue" | "dimgray" |
                "dodgerblue" | "feldspar" | "firebrick" | "floralwhite" |
                "forestgreen" | "fuchsia" | "gainsboro" | "ghostwhite" | "gold" |
                "goldenrod" | "gray" | "green" | "greenyellow" | "honeydew" |
                "hotpink" | "indianred" | "indigo" | "ivory" | "khaki" | "lavender" |
                "lavenderblush" | "lawngreen" | "lemonchiffon" | "lightblue" |
                "lightcoral" | "lightcyan" | "lightgoldenrodyellow" | "lightgreen" |
                "lightgrey" | "lightpink" | "lightsalmon" | "lightseagreen" |
                "lightskyblue" | "lightslateblue" | "lightslategray" |
                "lightsteelblue" | "lightyellow" | "lime" | "limegreen" | "linen" |
                "magenta" | "maroon" | "mediumaquamarine" | "mediumblue" |
                "mediumorchid" | "mediumpurple" | "mediumseagreen" |
                "mediumslateblue" | "mediumspringgreen" | "mediumturquoise" |
                "mediumvioletred" | "midnightblue" | "mintcream" | "mistyrose" |
                "moccasin" | "navajowhite" | "navy" | "oldlace" | "olive" |
                "olivedrab" | "orange" | "orangered" | "orchid" | "palegoldenrod" |
                "palegreen" | "paleturquoise" | "palevioletred" | "papayawhip" |
                "peachpuff" | "peru" | "pink" | "plum" | "powderblue" | "purple" |
                "red" | "rosybrown" | "royalblue" | "saddlebrown" | "salmon" |
                "sandybrown" | "seagreen" | "seashell" | "sienna" | "silver" |
                "skyblue" | "slateblue" | "slategray" | "snow" | "springgreen" |
                "steelblue" | "tan" | "teal" | "thistle" | "tomato" | "turquoise" |
                "violet" | "violetred" | "wheat" | "white" | "whitesmoke" | "yellow" |
                "yellowgreen" | "transparent"
            ] [
                value
            ]
        ]
        uri: func [value opts] [parse-uri/relative value]
        cdata: func [value opts] [if value [trim/lines value]]
        media-desc: func [value opts /local names nm] [
            names: clear [ ]
            if all [value parse/all trim/lines value [
                copy nm name (append names nm)
                any [
                    thru "," opt " " copy nm name (append names nm)
                ]
                to end
            ]] [
                names: intersect names [
                    "screen" "tty" "tv" "projection" "handheld"
                    "print" "braille" "aural" "all"
                ]
                if empty? names [return none]
                value: copy first names
                foreach name next names [
                    repend value [", " name]
                ]
                value
            ]
        ]
        list-style: func [value opts] [
            if find ["disc" "circle" "square"] value [return value]
            select/case [
                "1" "decimal"     #[none]
                "a" "lower-alpha" #[none]
                "A" "upper-alpha" #[none]
                "i" "lower-roman" #[none]
                "I" "upper-roman" #[none]
            ] value
        ]
        number: func [value opts] [
            if parse/all trim/lines value [some digit] [value]
        ]
        pixels: func [value opts] [
            if parse/all trim/lines value [some digit] [append value "px"]
        ]
        bool: func [value opts] [
            opts/1
        ]
        length: func [value opts] [
            if parse/all trim/lines value [some digit opt "%"] [value]
        ]
        lengthpx: func [value opts] [
            if parse/all trim/lines value [some digit ["%" | (append value "px") to end]] [value]
        ]
        multi-length: func [value opts] [
            if parse/all trim/lines value [some digit ["%" | "*" | none]] [value]
        ]
        font-size: func [value opts] [
            trim/lines value
            any [
                select [
                    "1"  "xx-small" #[none]
                    "2"  "x-small"  #[none]
                    "3"  "small"    #[none]
                    "4"  "medium"   #[none]
                    "5"  "large"    #[none]
                    "6"  "x-large"  #[none]
                    "7"  "xx-large" #[none]
                    "+1" "120%"     #[none]
                    "+2" "144%"     #[none]
                    "+3" "173%"     #[none]
                    "+4" "207%"     #[none]
                    "+5" "249%"     #[none]
                    "+6" "299%"     #[none]
                    "+7" "358%"     #[none]
                    "-1" "80%"      #[none]
                    "-2" "64%"      #[none]
                    "-3" "51%"      #[none]
                    "-4" "41%"      #[none]
                    "-5" "33%"      #[none]
                    "-6" "26%"      #[none]
                    "-7" "21%"      #[none]
                ] value
                length value none
            ]
        ]
        imgmargin: func [value opts] [
            if value: number value none [
                repend style [
                    "margin-" opts/1 ": " value "px;"
                    "margin-" opts/2 ": " value "px;"
                ]
            ]
            none
        ]
        imgalign: func [value opts] [
            append style any [select [
                "bottom" "vertical-align: bottom;" #[none]
                "middle" "vertical-align: middle;" #[none]
                "top"    "vertical-align: top;"    #[none]
                "left"   "float: left;"            #[none]
                "right"  "float: right;"           #[none]
            ] value ""]
            none
        ]
        brclear: func [value opts] [
            append style any [select [
                "none"  "clear: none;"  #[none]
                "left"  "clear: left;"  #[none]
                "right" "clear: right;" #[none]
                "all"   "clear: both;"  #[none]
            ] value ""]
            none
        ]
    ]
    process-attr: func [value type defvalue /local opts] [
        if defvalue = 'none [defvalue: none]
        if none? value [return defvalue]
        if block? type [opts: next type type: first type]
        type: get in attr-types type
        value: any [type value opts defvalue]
    ]

===Tree rewriting pass

    -tree-rewriting-:
    rewrite-tree root either current-options/target = 'editor [
        editor-rewrite-rules
    ] [
        xhtml-rewrite-rules
    ]

We need:

    -stage3-:
    xhtml-rewrite-rules: [
        -rewrite-rules-
    ]
    editor-rewrite-rules: head insert [
        -editor-rewrite-rules-
    ] xhtml-rewrite-rules

---Rewrite rules

    -rewrite-rules-:
    ; <i,u,b><br />^/</i,u,b> ---> <br />^/
    [[i u b] [] [br []] [text [value: "^/"]]] unwrap
    ; remove empty i, u, b, strike, span
    [[i u b strike span] []] remove
    ; unwrap span with no attributes
    [span [dir: #[none] id: #[none] class: #[none] title: #[none] style: #[none]] **] unwrap
    ; merge consecutive <u>, <i>, <span> 
    [u [] **] followed by [u prev **] rewrite to [u [*: *] ** next unwrapped]
    [i [] **] followed by [i prev **] rewrite to [i [*: *] ** next unwrapped]
    [span [] **] followed by [span prev **] rewrite to [span [*: *] ** next unwrapped]

---Rewrite rules for the |editor| target

    -editor-rewrite-rules-:
    ; <strike>...</strike> -> <span style="text-decoration: line-through;">...</span>
    [strike [] **] [span [style: "text-decoration: line-through;"] **]
    ; <u>...</u> -> <span style="text-decoration: underline;">...</span>
    [u [] **] [span [style: "text-decoration: underline;"] **]
    ; remove surious divs that may be created
    [blockquote/div [dir: #[none] id: #[none] class: #[none] title: #[none] style: #[none]] **] unwrap
    ; remove some spurious <br />s generated by FF
    [[li p] [] ** [br []] [text [value: "^/"]]] [* [*: *] 1 to -3]
    ; merge consecutive <blockquote>s
    [blockquote [] **] followed by newline-node: [text [value: "^/"]] followed by next-blockquote: [blockquote [] **]
        rewrite to [blockquote [] ** newline-node unwrapped next-blockquote unwrapped]

===Generation of XHTML text

    -emitter-:
    set-ml-encoding pick [html-utf8 html-ascii] to logic! current-options/emit-utf8
    emit make string! 1024 [
        either current-options/all [
            <?xml version="1.0" encoding="UTF-8"?> newline
            <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd"> newline
            emit-childs root
        ] [
            emit-childs select-child root 'html/body
        ]
    ]

---Functions used to emit XHTML

    -emitter-functions-:
    emit-childs: macro/custom [type cdata?] [
        [/cdata (cdata?: yes) | (cdata?: no)] do-next (
            foreach-child node pop-result [
                emit output [
                    switch type: node-type? node [
                        text [either cdata? [(get-property node 'value)] [text get-property node 'value]]
                        comment [(get-property node 'value)]
                        style [
                            tag 'style fix-attrs node-properties? node [
                                cdata/options [emit-childs /cdata node] [commented: yes]
                            ]
                        ]
                        /default [
                            tag type fix-attrs node-properties? node either find [base link hr area input img br col] type ['empty] [[emit-childs node]]
                        ]
                    ]
                ]
            ]
        )
    ]
    fix-attrs: func [attributes /local lang id] [
        if lang: select attributes 'lang [
            insert insert/only tail attributes 'xml/lang lang
        ]
        if all [pos: find attributes 'id current-options/id-prefix] [
            pos/2: join current-options/id-prefix pos/2
        ]
        attributes
    ]
