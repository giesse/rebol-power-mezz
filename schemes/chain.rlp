Chain port scheme for REBOL

    Purpose: {
        Implements chain://, a port scheme that allows chaining other (filter) ports together
        so that they are seen as a single port.
    }
    Author: "Gabriele Santilli"
    File: %chain.r
    License: "(C) 2008 Qtask, Inc. All rights reserved."
    Version: 1.0.0

===Introduction

===Overview

    -main-:
    net-utils/net-install 'chain make Root-Protocol [
        -support-functions-
        
        init: func [port spec] [
            -init-chain-
        ]
        open: func [port] [
            -open-chain-
        ]
        close: func [port] [
            -close-chain-
        ]
        write: func [port data] [
            -write-chain-
        ]
        read: func [port data] [
            -read-chain-
        ]
        update: func [port] [
            -update-chain-
        ]
    ] 80

===Implementation

---Initialize the chain port |port| from |spec|

    -init-chain-:
    if url? spec [
        net-error "Cannot make a chain port from url!"
    ]
    port/url: spec
    unless all [block? port/sub-port 1 < length? port/sub-port] [
        net-error "You must specify a list of ports to stream the data through"
    ]

---Open the chain port |port|

    -open-chain-:
    port/state/flags: port/state/flags or system/standard/port-flags/direct

---Close the chain port |port| (do nothing)

    -close-chain-:
    port

---Stream |data| through the list of ports

    -write-chain-:
    propagate-data port/sub-port copy/part data port/state/num
    port/state/num


---Read from the last port in the list

    -read-chain-:
    read-io last port/sub-port data port/state/num

---Update all the ports in the list

    -update-chain-:
    update-all port/sub-port

---Support functions

    -support-functions-:
    propagate-data: func [ports data /local filtered] [
        while [not tail? next ports] [
            if filtered: copy ports/1 [
                insert ports/2 filtered
            ]
            ports: next ports
        ]
        ports: head ports
        insert ports/1 data
    ]
    update-all: func [ports /local data] [
        while [not tail? next ports] [
            system/words/update ports/1
            if data: copy ports/1 [
                insert ports/2 data
            ]
            ports: next ports
        ]
        system/words/update ports/1
    ]
